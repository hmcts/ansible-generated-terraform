# The existing server modules were 90% the same and repeated so didn't want to use them
# this should fit all use cases and only one place to change code

# turn vm_name variable into vm_name with count at the end (2 numbers e.g. 01)
data "template_file" "server_name" {
  template = "$${prefix}${format("%02d", count.index + 1)}"

  count = "${var.instance_count}"

  vars {
    prefix = "${var.vm_name}"
  }
}

# Create Networking
resource "azurerm_network_interface" "reform-nonprod" {
  count               = "${var.instance_count}"
  name                = "${element(data.template_file.server_name.*.rendered, count.index)}-NIC"
  location            = "${var.location}"
  resource_group_name = "${var.resource_group}"

  ip_configuration {
    name                          = "${element(data.template_file.server_name.*.rendered, count.index)}-NIC"
    subnet_id                     = "${var.subnet_id}"
    private_ip_address_allocation = "dynamic"
  }
}

resource "azurerm_virtual_machine" "reform-nonprod" {
  count                 = "${var.instance_count}"
  name                  = "${element(data.template_file.server_name.*.rendered, count.index)}"
  location              = "${var.location}"
  resource_group_name   = "${var.resource_group}"
  network_interface_ids = ["${element(azurerm_network_interface.reform-nonprod.*.id, count.index)}"]
  vm_size               = "${var.vm_size}"
  availability_set_id   = "${var.avset_id}"

  storage_image_reference {
    publisher = "OpenLogic"
    offer     = "CentOS"
    sku       = "7.3"
    version   = "latest"
  }

  storage_os_disk {
    name          = "${element(data.template_file.server_name.*.rendered, count.index)}"
    vhd_uri       = "https://${var.storage_account}.blob.core.windows.net/vhds/${element(data.template_file.server_name.*.rendered, count.index)}.vhd"
    caching       = "ReadWrite"
    create_option = "FromImage"
  }

  storage_data_disk {
    name              = "${element(data.template_file.server_name.*.rendered, count.index)}-DATA00"
    vhd_uri           = "https://${var.storage_account}.blob.core.windows.net/vhds/${element(data.template_file.server_name.*.rendered, count.index)}-DATA00.vhd"
    create_option     = "Empty"
    caching						=	"ReadWrite"
    lun               = 0
    disk_size_gb      = "${var.additional_disk_size_gb}"
  }

  os_profile {
    computer_name  = "${element(data.template_file.server_name.*.rendered, count.index)}"
    admin_username = "<lookedupfromvault>"
    admin_password = "<lookedupfromvault>"
  }

  os_profile_linux_config {
    disable_password_authentication = true

    ssh_keys {
      path     = "/home/<lookedup>/.ssh/authorized_keys"
      key_data = "${var.ssh_key}"
    }
  }

  boot_diagnostics {
    enabled     = true
    storage_uri = "https://${var.diagnostics_storage_account}.blob.core.windows.net/"
  }

  tags {
    type      = "vm"
    product   = "${var.product}"
    env       = "${var.env}"
    tier      = "${var.tier}"
    ansible   = "${var.ansible}"
    terraform = "true"
    role      = "${var.role}"
  }
}

resource "azurerm_virtual_machine_extension" "script" {
  count                = "${var.instance_count}"
  name                 = "${element(data.template_file.server_name.*.rendered, count.index)}"
  location             = "${var.location}"
  resource_group_name  = "${var.resource_group}"
  virtual_machine_name = "${element(data.template_file.server_name.*.rendered, count.index)}"
  publisher            = "Microsoft.Azure.Extensions"
  type                 = "CustomScript"
  type_handler_version = "2.0"
  depends_on           = ["azurerm_virtual_machine.reform-nonprod"]

  settings = <<SETTINGS
    {
        "commandToExecute": "iptables -t nat -A PREROUTING -p tcp --dport 868 -j REDIRECT --to-ports 22; iptables-save > /etc/sysconfig/iptables"
    }
SETTINGS
}
